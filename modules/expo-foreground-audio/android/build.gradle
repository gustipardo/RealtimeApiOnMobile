apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'expo.modules.foregroundaudio'
version = '1.0.0'

buildscript {
  def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
  if (expoModulesCorePlugin.exists()) {
    apply from: expoModulesCorePlugin
    applyKotlinExpoModulesCorePlugin()
  }
}

afterEvaluate {
  publishing {
    publications {
      release(MavenPublication) {
        from components.release
      }
    }
    repositories {
      maven {
        url = mavenLocal().url
      }
    }
  }
}

android {
  namespace "expo.modules.foregroundaudio"
  compileSdkVersion safeExtGet("compileSdkVersion", 35)

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 26)
    targetSdkVersion safeExtGet("targetSdkVersion", 35)
    versionCode 1
    versionName "1.0.0"
  }

  lintOptions {
    abortOnError false
  }

  publishing {
    singleVariant("release") {
      withSourcesJar()
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_17.majorVersion
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation project(':expo-modules-core')
  implementation 'androidx.core:core-ktx:1.13.1'
  implementation 'androidx.media:media:1.7.0'
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}
